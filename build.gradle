plugins {
    id 'java'
    id 'maven-publish'
    id "com.gradleup.shadow" version "9.2.2"
    id "io.papermc.paperweight.userdev" version "2.0.0-beta.19"
}

group = 'com.github.vermulst'
version = '1.2.0'


def deploymentProperties = new Properties()
def propertiesFile = rootProject.file("local.properties")

if (propertiesFile.exists()) {
    propertiesFile.withInputStream { deploymentProperties.load(it) }
} else {
    logger.warn("local.properties not found. The 'deploy' task will not work.")
}


repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    mavenLocal()
}

dependencies {
    paperweight.paperDevBundle("1.21.3-R0.1-SNAPSHOT")
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}


def targetJavaVersion = 21
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "https://jitpack.io"
        }
    }
}


tasks.register('deploy') {
    // Set the task to depend on shadowJar
    dependsOn shadowJar

    doLast {
        // Only proceed if deploymentDir is set in local.properties
        def deploymentDirValue = deploymentProperties.getProperty('deploymentDir')

        if (deploymentDirValue) {
            copy {
                from shadowJar.archiveFile
                into deploymentDirValue
                // Optional: Print the path for confirmation
                println "MultiBreak JAR copied to: ${deploymentDirValue}"
            }
        } else {
            println "deploymentDir is not set in local.properties. Skipping deploy."
        }
    }
}

shadowJar {
    archiveFileName = "MultiBreak-${version}.jar"
    relocate 'org.bstats', 'me.vermulst.multibreak.dependencies.bstats'
}

